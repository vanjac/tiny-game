SPR r6 9 9  @ dot x

.lbl start

SPR r0 7 7  @ snake xy
SPR r2 0 0  @ head/tail direction (right, down, left, up, ...)
SPR r4 3 7  @ snake tail x

.lbl initdot
INC r7
@ check if something is already there
RPX r6 0
TOG f1
IFF f1
    JMP initdot
    NOP
DPX r6 3
CLF f0

.lbl gameloop

SET r9 2 @ wait for this many frames + 1
CLF f5
.lbl framewait
IFF f5
    JMP framewaitnext
    NOP
INC r6 @ move dot X
JMP framewait

.lbl framewaitnext
INC r7 @ move dot Y
@ turn buttons
IFF f8
    DEC r2
    DPX r0 2
    NOP
IFF f9
    INC r2
    DPX r0 3
    NOP
CLF f0
DEC r9
IFF f0
    JMP gameloop1
    NOP
JMP framewait

.lbl gameloop1

CLF f2
.lbl move1

DEC r2
IFF f0
    SET r2 0
    TOG f2
IFF f2
    INC r0
    JMP move2
DEC r2
IFF f0
    SET r2 1
    TOG f2
IFF f2
    INC r1
    JMP move2
DEC r2
IFF f0
    SET r2 2
    TOG f2
IFF f2
    DEC r0
    JMP move2
DEC r2
IFF f0
    SET r2 3
    TOG f2
IFF f2
    DEC r1
    JMP move2

JMP move1

.lbl move2
IFF f0
    @ hit screen edge
    JMP end
    NOP

RPX r0 3
IFF f1
    @ eat dot (don't move tail)
    DPX r0 1
    JMP initdot
RPX r0 0
TOG f1
IFF f1
    JMP end
    NOP
DPX r0 1

CLF f2
.lbl tailmove1

DEC r3
IFF f0
    SET r3 0
    TOG f2
IFF f2
    INC r4
    JMP tailmove2
DEC r3
IFF f0
    SET r3 1
    TOG f2
IFF f2
    INC r5
    JMP tailmove2
DEC r3
IFF f0
    SET r3 2
    TOG f2
IFF f2
    DEC r4
    JMP tailmove2
DEC r3
IFF f0
    SET r3 3
    TOG f2
IFF f2
    DEC r5
    JMP tailmove2

JMP tailmove1

.lbl tailmove2
CLF f0

RPX r4 2
IFF f1
    DEC r3
    NOP
    NOP
RPX r4 3
IFF f1
    INC r3
    NOP
    NOP
CLF f0

DPX r4 0

JMP gameloop

.lbl end

SET r0 0
.align 16
.lbl fastfill
DPX r15 0
DPX r15 0
DPX r15 0
DPX r15 0
DPX r15 0
DPX r15 0
DPX r15 0
DPX r15 0
DPX r15 0
DPX r15 0
DPX r15 0
DPX r15 0
DPX r15 0
DPX r15 0
DPX r15 0
DPX r15 0
INC r0
IFF f0
    JMP start
    NOP
JMP fastfill